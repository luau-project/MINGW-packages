# Maintainer: luau-project <luau.project@gmail.com>

_realname=luafilesystem
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-lua-${_realname}"
         "${MINGW_PACKAGE_PREFIX}-lua51-${_realname}"
         "${MINGW_PACKAGE_PREFIX}-lua53-${_realname}")
pkgver=1.8.0
pkgrel=1
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/lunarmodules/luafilesystem'
license=('spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-lua"
             "${MINGW_PACKAGE_PREFIX}-lua51"
             "${MINGW_PACKAGE_PREFIX}-lua53"
             "${MINGW_PACKAGE_PREFIX}-pkgconf")
source=("${url}/archive/refs/tags/v${pkgver//./_}.tar.gz"
        "CMakeLists.txt"
        "MSYS2-GetLuaInfoFromPkgConfig-v1.0.0.cmake")
sha256sums=('16d17c788b8093f2047325343f5e9b74cccb1ea96001e45914a58bbae8932495'
            'dd85079531c556d4456d668ddf07289ec7c260947700ea4a284b24548f3d0d0d'
            '7c66dddca6bda0dc53ef2ac7ee292958120aef0a3104538d6636b413f013aac0')

prepare() {
  cp ${srcdir}/CMakeLists.txt "${srcdir}/${_realname}-${pkgver//./_}"
  cp ${srcdir}/MSYS2-GetLuaInfoFromPkgConfig-v1.0.0.cmake "${srcdir}/${_realname}-${pkgver//./_}"
}

build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  # current Lua
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -DIS_CURRENT_VERSION=ON \
      -S "${_realname}-${pkgver//./_}" \
      -B "build-${MSYSTEM}"

  "${MINGW_PREFIX}"/bin/cmake.exe --build "build-${MSYSTEM}"

  # Lua 5.1
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -DLUA_VERSION=5.1 \
      -S "${_realname}-${pkgver//./_}" \
      -B "build-${MSYSTEM}-51"

  "${MINGW_PREFIX}"/bin/cmake.exe --build "build-${MSYSTEM}-51"

  # Lua 5.3
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -DLUA_VERSION=5.3 \
      -S "${_realname}-${pkgver//./_}" \
      -B "build-${MSYSTEM}-53"

  "${MINGW_PREFIX}"/bin/cmake.exe --build "build-${MSYSTEM}-53"
}

check() {
  "${MINGW_PREFIX}"/bin/cmake.exe --build "build-${MSYSTEM}" --target test

  "${MINGW_PREFIX}"/bin/cmake.exe --build "build-${MSYSTEM}-51" --target test

  "${MINGW_PREFIX}"/bin/cmake.exe --build "build-${MSYSTEM}-53" --target test
}

package_lua-luafilesystem() {
  pkgdesc="File System Library for the Lua Programming Language (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-lua")

  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install "build-${MSYSTEM}"

  install -Dm644 "${srcdir}/${_realname}-${pkgver//./_}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/lua-${_realname}/LICENSE"
}

package_lua51-luafilesystem() {
  pkgdesc="File System Library for the Lua 5.1 Programming Language (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-lua51")

  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install "build-${MSYSTEM}-51"

  install -Dm644 "${srcdir}/${_realname}-${pkgver//./_}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/lua51-${_realname}/LICENSE"
}

package_lua53-luafilesystem() {
  pkgdesc="File System Library for the Lua 5.3 Programming Language (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-lua53")

  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install "build-${MSYSTEM}-53"

  install -Dm644 "${srcdir}/${_realname}-${pkgver//./_}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/lua53-${_realname}/LICENSE"
}

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
